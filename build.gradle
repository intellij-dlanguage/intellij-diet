import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'org.jetbrains.intellij.platform' version '2.7.2'
    id 'org.jetbrains.kotlin.jvm' version '2.2.10' // This should match the Kotlin version in gradle.properties.
    id 'org.jetbrains.grammarkit' version '2022.3.2.2'
}


sourceSets {
    main {
        java.srcDirs 'src/main/kotlin'
        resources.srcDirs 'src/main/resources'
    }
    test {
        java.srcDirs 'src/test/kotlin'
    }
}

group 'com.singingbush'
version = "${version}"

// --- code gen stuff
apply plugin: 'java'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:deprecation"
}

def genRoot = file('build/gen')

apply plugin: 'kotlin'

kotlin {
    jvmToolchain(21)
}

sourceSets {
    main.java.srcDirs = files(genRoot) // we only have generated Java source
    main.resources.srcDirs = files("src/main/resources")
}

apply plugin: 'org.jetbrains.grammarkit'
apply plugin: 'org.jetbrains.intellij.platform'

generateParser {
    sourceFile = file("grammar/diet.bnf")
    targetRootOutputDir = file('build/gen')
    pathToParser = '/com/singingbush/plugins/diet/parser/DietParser.java'
    pathToPsiRoot = '/com/singingbush/plugins/diet/psi'
    purgeOldFiles = true
}

generateLexer {
    sourceFile = file("grammar/_DietLexer.flex")
    targetOutputDir = file("build/gen/com/singingbush/plugins/diet/parser/")
    targetClass = "_DietLexer"
    purgeOldFiles = false
}

project.generateLexer.dependsOn generateParser
project.compileJava.dependsOn generateLexer
project.compileKotlin.dependsOn generateLexer
// --- end the code gen stuff


allprojects {

    tasks.withType(Test) {
        useJUnit {
            include '**/**/*Test.*' // any Java or Kotlin class that ends with 'Test'
        }

        testLogging {
            beforeSuite { suite ->
                if (!suite.parent) { // will match the outermost suite
                    logger.lifecycle ' ----------- Building Tests -----------'
                } else if (suite.className != null) {
                    logger.lifecycle "${suite.className}:"
                }
            }

            afterTest { descriptor, result ->
                switch (result.resultType) {
                    case 'SUCCESS':
                    case 'PASSED':
                        logger.lifecycle("\t✔ {}", descriptor.name)
                        break
                    case TestResult.ResultType.SKIPPED.name():
                        logger.lifecycle("\t- {}", descriptor.name)
                        break
                    case TestResult.ResultType.FAILURE.name():
                        logger.lifecycle("\t✘ {}", descriptor.name)
                        break
                    default:
                        logger.lifecycle("\t? {} {}", descriptor.name, result.resultType)
                }
            }

            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                    def startItem = '|  ', endItem = '  |'
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength) + '\n')
                }
            }
        }
    }

    // take the version number defined in gradle build and use that in plugin.xml
    task initConfig(type: Copy) {
        from('src/main/resources') {
            include '**/plugin.xml'
            filter(ReplaceTokens, tokens: [version: version])
        }
    }

    intellijPlatform {
        projectName = 'intellij-diet'
        publishPlugin {
            token = provider {
                System.getenv('JETBRAINS_TOKEN')
            }
            channels = [publishChannels]
        }
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

repositories {
    mavenCentral()

    intellijPlatform {
    	defaultRepositories()
    }
}

dependencies {
    //testCompile 'io.kotlintest:kotlintest:1.3.3'

    intellijPlatform {
    	// This plugin relies on the JavaScript plugin which is not bundled with IC, so the plugin requires IU
        create('IU', ideaVersion) // (e.g., "IC", "IU", "CL", etc.)
        bundledPlugins 'org.intellij.intelliLang', 'JavaScript'
        plugins 'net.masterthought.dlanguage:1.37.3', 'com.jetbrains.plugins.jade:243.21565.129'
    }
}

apply plugin: 'idea'
idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
        vcs = 'Git'
    }
    module {
        generatedSourceDirs += genRoot
    }
}
