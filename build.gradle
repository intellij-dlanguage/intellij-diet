import org.apache.tools.ant.filters.ReplaceTokens
import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

buildscript {
    repositories {
        mavenCentral()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
        maven { url 'https://jitpack.io' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.hurricup:gradle-grammar-kit-plugin:2017.1.1"
    }
}

plugins {
    id 'org.jetbrains.intellij' version '0.2.18'
}


sourceSets {
    main {
        java.srcDirs 'src/main/kotlin'
        resources.srcDirs 'src/main/resources'
    }
    test {
        java.srcDirs 'src/test/kotlin'
    }
}

group 'com.singingbush'
version = "${version}"

// --- code gen stuff
apply plugin: 'java'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:deprecation"
}

def genRoot = file('build/gen')

apply plugin: 'kotlin'

sourceSets {
    main.java.srcDirs = files(genRoot) // we only have generated Java source
}

apply plugin: 'org.jetbrains.grammarkit'

task generateDietParser(type: GenerateParser) {
    source = "grammar/diet.bnf"
    targetRoot = 'build/gen'
    pathToParser = '/com/singingbush/plugins/diet/parser/DietParser.java'
    pathToPsiRoot = '/com/singingbush/plugins/diet/psi'
    purgeOldFiles = true
}

task generateDietLexer(type: GenerateLexer) {
    source = "grammar/_DietLexer.flex"
    targetDir = "build/gen/com/singingbush/plugins/diet/parser/"
    targetClass = "_DietLexer"
    purgeOldFiles = true
}

project.generateDietLexer.dependsOn generateDietParser
project.compileJava.dependsOn generateDietLexer
project.compileKotlin.dependsOn generateDietLexer
// --- end the code gen stuff


allprojects {

    tasks.withType(Test) {
        useJUnit {
            include '**/**/*Test.*' // any Java or Kotlin class that ends with 'Test'
        }

        testLogging {
            beforeSuite { suite ->
                if (!suite.parent) { // will match the outermost suite
                    logger.lifecycle ' ----------- Building Tests -----------'
                } else if (suite.className != null) {
                    logger.lifecycle "${suite.className}:"
                }
            }

            afterTest { descriptor, result ->
                switch (result.resultType) {
                    case 'SUCCESS':
                    case 'PASSED':
                        logger.lifecycle("\t✔ {}", descriptor.name)
                        break
                    case TestResult.ResultType.SKIPPED.name():
                        logger.lifecycle("\t- {}", descriptor.name)
                        break
                    case TestResult.ResultType.FAILURE.name():
                        logger.lifecycle("\t✘ {}", descriptor.name)
                        break
                    default:
                        logger.lifecycle("\t? {} {}", descriptor.name, result.resultType)
                }
            }

            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                    def startItem = '|  ', endItem = '  |'
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength) + '\n')
                }
            }
        }
    }

    // take the version number defined in gradle build and use that in plugin.xml
    task initConfig(type: Copy) {
        from('src/main/resources') {
            include '**/plugin.xml'
            filter(ReplaceTokens, tokens: [version: version])
        }
    }

    apply plugin: 'org.jetbrains.intellij'
    intellij {
        pluginName 'diet'
        version ideaVersion
        // Pug (ex-Jade) = jade:171.1834.8
        // markdown:2017.1.20170119
        // Should work in: IntelliJ IDEA PhpStorm WebStorm PyCharm RubyMine AppCode CLion Gogland Rider
        plugins 'net.masterthought.dlanguage:1.15.1', 'com.jetbrains.plugins.jade:172.2656.13', 'CSS', 'JavaScriptLanguage' // use the PUG (ex-Jade) plugin for providing basic syntax highlighting
        type 'IU' // required as cannot run plugin in Community Edition because PUG (ex-Jade) plugin needs 'com.intellij.css:171.4249.39' and 'JavaScript:1.0' which are not in the repo
        //localPath '/Applications/IntelliJ IDEA.app/Contents' // if using this, make sure to remove 'version'

        publishPlugin {
            username System.getenv('JETBRAINS_USERNAME')
            password System.getenv('JETBRAINS_PASSWORD')
            channels publishChannels
        }
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    //compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version" // will also pull in stdlib
    //runtime 'org.jetbrains.plugins:net.masterthought.dlanguage' // for basic D stuff
    //runtime 'org.jetbrains.plugins:com.jetbrains.plugins.jade' // for diet templates
    //testCompile 'io.kotlintest:kotlintest:1.3.3'
}

apply plugin: 'idea'
idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
        vcs = 'Git'
    }
    module {
        generatedSourceDirs += genRoot
    }
}
